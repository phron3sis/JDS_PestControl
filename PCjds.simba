{$UNDEF SCRIPT_ID}{$DEFINE SCRIPT_ID := 'pest_control_jds'}
{$UNDEF SCRIPT_REVISION}{$DEFINE SCRIPT_REVISION := '6'}
{$DEFINE SCRIPT_GUI}

program WaspPestControl;

{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

const
  DEBUG_SHIFTERS    = True; //Debug shifter points/boxes/click circles
  PointsPerGame     =   4;  //Depends on diary and boat
  SHIFTER_LOG_FILE  = 'pest_shifters_log.csv'; // simple CSV in working dir

  // Boat interiors
  VETERAN_BOAT_BOX     : TBox = [6105, 4695, 6120, 4718];
  INTER_BOAT_BOX       : TBox = [6129, 4723, 6144, 4746];
  NOVICE_BOAT_BOX      : TBox = [6218, 4740, 6233, 4763];

  // Plank tiles
  VETERAN_PLANK_TILE   : TPoint = [6130, 4700];
  INTER_PLANK_TILE     : TPoint = [6154, 4736];
  NOVICE_PLANK_TILE    : TPoint = [6206, 4756];

  // Island areas
  DEFEND_KNIGHT_BOX    : TBox = [5741, 5605, 5775, 5639];
  GAME_START_LANDING_BOX: TBox = [5752, 5532, 5773, 5562];

{********************************************}
{                 ENUM STATES                }
{********************************************}

type
  EPestState = (
    PCS_IN_BOAT,
    PCS_WALK_CENTER,
    PCS_DEFEND,
    PCS_AT_PLANK,
    PCS_UNKNOWN,
    PCS_END_SCRIPT
  );


{********************************************}
{                 SCRIPT RECORD              }
{********************************************}

type
  TJdsPestControl = record(TBaseWalkerScript)
    State            : EPestState;

    CenterTile       : TPoint;
    PlankTile        : TPoint;
    CrossPlankTile   : TPoint;
    TheGangPlank     : TRSObject;

    LastXP           : Int64;
    LastXPTime       : UInt64;
    NextAttackDelay  : Int32;

    StartTime        : UInt64;

    // ===   TRACKING   ===
    GamesWon      : UInt32;
    LastWinTime   : UInt64;
    WinCooldown   : UInt64;

    PointsTotal    : UInt32;
    PointsTime     : UInt64;

    OurCmbtLvl     : UInt32;
    OurCmbtStyle   : String;
  end;

  {********************************************}
  {                 HUD RECORD                 }
  {********************************************}

  TPestHUD = record
    LastUpdate: UInt64;
    Text      : TStringArray;
  end;

  // === SHIFTER DETECTION TYPES ===
  TShifterDetection = record
    Mean: TPoint;
    Cluster: TPointArray;     // primary (C1)
    Secondary: TPointArray;   // secondary (C2)
    ABox: TBox;
  end;

  TShifterDetectionArray = array of TShifterDetection;

var
  PestControl: TJdsPestControl;
  PestHUD    : TPestHUD;

procedure ApplyBioMouse();
var
  bh: Double;
begin
  bh := Login.GetPlayerBioHash();  // 0.00..1.00

  // === Mouse speed ===
  // 10–14 (safe human range)
  Mouse.Speed := Round(10 + ((bh - 0.5) * 4));
  Mouse.Speed := EnsureRange(Mouse.Speed, 9, 15);

  // === Gravity ===
  Mouse.Gravity := 8 + (bh * 3);

  // === Wind ===
  // 4–6 (smooth)
  Mouse.Wind := 4 + (bh * 2);

  // === Miss chance ===
  // 10–18% (human-like)
  Mouse.MissChance := 10 + (bh * 8);

  // === Distribution ===
  // GAUSS → ROWP → SKEWED
  if bh < 0.33 then
    Mouse.Distribution := MOUSE_DISTRIBUTION_GAUSS
  else if bh < 0.66 then
    Mouse.Distribution := MOUSE_DISTRIBUTION_ROWP
  else
    Mouse.Distribution := MOUSE_DISTRIBUTION_SKEWED;

  // === Idle interval ===
  Mouse.IdleInterval := 3.9 + (bh * 1.3);
end;


// --- BioHash-Aware Wait Override ---
procedure Wait(Min, Max: Double; Weight: EWaitDir = wdMean);override;
var
  adjMin, adjMax, bias: Double;
begin
  // BioHash → range [-0.05..+0.05]
  bias := (Login.GetPlayerBioHash() - 0.5) * 0.10;

  adjMin := Min + (Min * bias);
  adjMax := Max + (Max * bias);

  case Weight of
    wdLeft:
      System.Wait(Round(SRL.TruncatedGauss(adjMin, adjMax)));

    wdMean:
      System.Wait(Round(SRL.NormalRange(adjMin, adjMax)));

    wdRight:
      System.Wait(Round(SRL.TruncatedGauss(adjMax, adjMin)));
  end;
end;


function HumanGauss(minVal, maxVal, mean, stdev: Double; bioWeight: Double = 0): Int32;
var
  r: Double;
begin
  // Generate base Gaussian
  r := SRL.GaussRand(mean, stdev);

  // Add user-specific noise (zero-mean, does not increase range)
  if bioWeight <> 0 then
    r += (BioHash - 0.5) * bioWeight;

  // Clamp inside range
  if r < minVal then r := minVal;
  if r > maxVal then r := maxVal;

  // Convert to ms
  Result := Round(r * 1000);
end;

function TimeUntilNextBreak(): String;
var
  i: Int32;
  soonest: PBreakTask;
  remaining: Int64;
begin
  // No breaks scheduled
  if Length(Antiban.Breaks) = 0 then
    Exit('N/A');

  // Initialize soonest pointer
  soonest := @Antiban.Breaks[0];

  // Find the break with the smallest NextAtTime
  for i := 1 to High(Antiban.Breaks) do
    if Antiban.Breaks[i].NextAtTime < soonest^.NextAtTime then
      soonest := @Antiban.Breaks[i];

  // Time until it fires (in ms)
  remaining := Round(soonest^.NextAtTime - GetTimeRunning());
  if remaining < 0 then
    remaining := 0;

  // Format like "02m 58s"
  Result := SRL.MsToTime(remaining, Time_Short).Trim();
end;

{********************************************}
{                ANTIBAN SETUP               }
{********************************************}

procedure TAntiban.Setup(); override;
begin
  Self.Skills := [
    ERSSkill.ATTACK,
    ERSSkill.STRENGTH,
    ERSSkill.DEFENCE,
    ERSSkill.HITPOINTS,
    ERSSkill.TOTAL
  ];

  Self.MinZoom := 25;
  Self.MaxZoom := 50;

  inherited;
end;


{********************************************}
{                 LOGGING                    }
{********************************************}
procedure TJdsPestControl.Msg(const s: String);
var
  t: UInt64;
  h, m, sec, ms: UInt64;
begin
  t := GetSystemTime() - Self.StartTime;

  // Convert ms → hh:mm:ss:ms
  h   := t div 3600000;
  m   := (t div 60000) mod 60;
  sec := (t div 1000) mod 60;
  ms  := t mod 1000;

  WriteLn(Format('[%.2d:%.2d:%.2d:%.3d]:[PCjds]: %s', [h, m, sec, ms, s]));
end;


procedure TPestHUD.UpdateAndDraw();
const
  BG_COLOR      = $151515;
  BORDER_COLOR  = $CC2A2A;
  HEADER_COLOR  = $D6B43A;
  TEXT_COLOR    = $E0E0E0;
var
  elapsed: UInt64;
  winsPerHour: Double;
  i, lineHeight: Int32;
  abox: TBox;
  x, y: Int32;
begin
  // Run only every X amount of MS
  if (GetSystemTime - LastUpdate) < 1000 then
    Exit;
  LastUpdate := GetSystemTime;

  wait(20);
  // ==== COMPUTE HUD TEXT ====
  Text := [];

  elapsed := PestControl.TimeRunning.ElapsedTime();
  winsPerHour := NumberPerHour(PestControl.GamesWon, elapsed);

  Text += 'Pest Control - by Footballjds | '+ToStr(PestControl.PointsTotal);
  Text += 'Runtime: ' + SRL.MsToTime(elapsed, Time_Short).Trim();
  Text += 'State: ' + ToStr(PestControl.State);
  Text += 'Wins: ' + IntToStr(PestControl.GamesWon);
  Text += 'Wins/hr: ' + SRL.FormatNumber(winsPerHour, 2);
  Text += 'Points/hr: ' + SRL.FormatNumber(winsPerHour * PointsPerGame, 2);
  Text += 'Total EXP: ' + SRL.FormatNumber(XPBar.TotalEarnedXP(), 2);
  Text += 'XP/hr: ' + SRL.FormatNumber(
                     NumberPerHour(XPBar.TotalEarnedXP(), elapsed), 2
                   );
  Text += 'Next Break: ' + TimeUntilNextBreak();

  // ==== DRAW HUD ====
  lineHeight := 14;

  abox := Box(7, 345, 195, 473);

  RSClient.Image().DrawBoxFilled(abox.Expand(2), False, BORDER_COLOR);
  RSClient.Image().DrawBoxFilled(abox, False, BG_COLOR);

  RSClient.Image().SetFontName('Arial');
  RSClient.Image().SetFontSize(11);

  x := abox.X1 + 3;
  y := abox.Y1 + 3;

  for i := 0 to High(Text) do
  begin
    if i = 0 then
      RSClient.Image().DrawText(Text[i], Point(x, y), HEADER_COLOR)
    else
      RSClient.Image().DrawText(Text[i], Point(x, y), TEXT_COLOR);
    y += lineHeight;
  end;
end;



{********************************************}
{           XP STATE / ATTACK LOGIC          }
{********************************************}

procedure TJdsPestControl.UpdateXPState();
var
  xp: Int64;
begin
  xp := XPBar.Read();
  if xp <> Self.LastXP then
  begin
    Self.LastXP     := xp;
    Self.LastXPTime := GetSystemTime();
    Self.NextAttackDelay := Random(2299, 3600);
  end;
end;

function TJdsPestControl.XPStale(): Boolean;
begin
  Result := (GetSystemTime - Self.LastXPTime) > Self.NextAttackDelay;
end;

{********************************************}
{               STATE DETECTION              }
{********************************************}

function TJdsPestControl.InBoat(): Boolean;
var
  _ourPos: TPoint;
begin
  _ourPos := self.rsw.GetMyPos();

  result := _ourPos.InBox(VETERAN_BOAT_BOX) or
    _ourPos.InBox(INTER_BOAT_BOX) or
      _ourPos.InBox(NOVICE_BOAT_BOX);
end;

function TJdsPestControl.GetState(): EPestState;
var
  pos: TPoint;
begin
  pos := Self.RSW.GetMyPos();

  // ================================
  // OUTPOST (boats + planks)
  // ================================
  if pos.InBox(RSRegions.PEST_CONTROL_OUTPOST) then
  begin
    // Inside any of the 3 boats → waiting for game
    if pos.InBox(VETERAN_BOAT_BOX)
    or pos.InBox(INTER_BOAT_BOX)
    or pos.InBox(NOVICE_BOAT_BOX) then
      Exit(PCS_IN_BOAT);

    // At any plank → waiting to board or just returned from game
    if (Distance(pos, VETERAN_PLANK_TILE) <= 5)
    or (Distance(pos, INTER_PLANK_TILE)   <= 5)
    or (Distance(pos, NOVICE_PLANK_TILE)  <= 5) then
      Exit(PCS_AT_PLANK);

    // On outpost but not in boat or at plank
    Exit(PCS_AT_PLANK);  // treat as plank wait (safe default)
  end;


  // ================================
  // GAME ISLAND (actual minigame)
  // ================================
  if pos.InBox(RSRegions.PEST_CONTROL_ISLAND) then
  begin
    // Just spawned in landing boat → walk to center
    if pos.InBox(GAME_START_LANDING_BOX) then
      Exit(PCS_WALK_CENTER);

    // In knight defending box → defend
    if pos.InBox(DEFEND_KNIGHT_BOX) then
      Exit(PCS_DEFEND);

    // On island but not in defend box → walk to center
    Exit(PCS_WALK_CENTER);
  end;


  // ================================
  // Outside all known regions
  // ================================
  Result := PCS_UNKNOWN;
end;


{********************************************}
{                  ACTIONS                   }
{********************************************}

procedure TJdsPestControl.UpdatePoints();
var
  BluePoints: Int32;
  LastCheck : Int32 = 60000;
  PEST_POINTS_BOX: Tbox = [72, 64, 123, 81];
begin
  if GetSystemTime < (LastCheck+Self.PointsTime) then
    EXIT;
  BluePoints := ocr.RecognizeNumber(PEST_POINTS_BOX, TOCRColorFilter.Create([16777113]), RS_FONT_PLAIN_12);

  if BluePoints <= Self.PointsTotal then Exit;

  Self.PointsTotal := BluePoints;
  Self.PointsTime := GetSystemTime;
  Self.Msg('Total Points: '+toStr(Self.PointsTotal));
end;

procedure TJdsPestControl.DoInBoat();
begin
  self.UpdatePoints();
  Wait(300, 1500, wdleft);
end;

procedure TJdsPestControl.DoWalkCenter();
begin
  if Self.RSW.AtTile(Self.CenterTile, 15) then
  begin
    Minimap.EnablePrayer();
    Exit;
  end;

  if Antiban.BioDice(25) then
  begin
    Minimap.EnablePrayer();
    Wait(10, 230, wdLeft);
  end;

  Self.RSW.WalkBlind(Self.CenterTile.Random(-7,7,True), 10);
  Minimap.EnablePrayer();
end;

procedure TJdsPestControl.DebugFoundShifters(
  const detections: TShifterDetectionArray;
  const centerMS: TPoint;
  const radius: Int32
);
var
  sh: TShifterDetection;
  R: UInt32;
begin
  RSClient.Image.Clear();

  if MM2MS.GetZoomLevel() < 37 then
    R := 7
  else
    R := 11;

  for sh in detections do
  begin
    // draw primary cluster (yellow)
    RSClient.Image.DrawTPA(sh.Cluster, $FFFF00);

    // draw secondary (green)
    RSClient.Image.DrawTPA(sh.Secondary, $00FF00);

    // draw abox (orange)
    RSClient.Image.DrawBox(sh.ABox, $00A5FF);

    // draw mean point circle (cyan)
    RSClient.Image.DrawCircle(sh.Mean, R, $00FFFF);

    // counts label near the shifter
    RSClient.Image.DrawText(
      Format('P:%d S:%d', [sh.Cluster.Len, sh.Secondary.Len]),
      Point(sh.Mean.X + 8, sh.Mean.Y - 8),
      $FFFFFF
    );
  end;

  // search radius (pink)
  RSClient.Image.DrawCircle(centerMS, radius, $FF00FF);
end;

function TJdsPestControl.GetShifters(): TShifterDetectionArray;
type
  TColorPair = record
    C1, C2: TCTS2Color;
  end;

var
  Profiles: array of TColorPair;
  P: TColorPair;

  primaryTPA, secondTPA: TPointArray;
  clusters: T2DPointArray;
  c: TPointArray;
  abox: TBox;

  tileMS: TRectangle;
  centerMS: TPoint;
  zoom: Double;
  radius: Int32;

  detections: TShifterDetectionArray;
  sh: TShifterDetection;

const
  EXPAND_PX   = 7;
  MIN_PRIMARY = 7;
  MIN_SECOND  = 7;

  procedure ProcessProfile(const CP: TColorPair);
  begin
    primaryTPA := [];
    SRL.FindColors(primaryTPA, CP.C1, MainScreen.Bounds);
    if primaryTPA = [] then
      Exit;

    clusters := primaryTPA.Cluster(6);

    for c in clusters do
    begin
      if c.Len < MIN_PRIMARY then
        Continue;

      abox := c.Bounds().Expand(EXPAND_PX);
      abox := abox.EnsureRange(MainScreen.Bounds);

      secondTPA := [];
      SRL.FindColors(secondTPA, CP.C2, abox);

      if secondTPA.Len >= MIN_SECOND then
      begin
        sh.Mean      := c.Mean();
        sh.Cluster   := c;
        sh.Secondary := secondTPA;
        sh.ABox      := abox;

        if centerMS.DistanceTo(sh.Mean) <= radius then
        begin
          SetLength(detections, Length(detections) + 1);
          detections[High(detections)] := sh;
        end;
      end;
    end;
  end;

begin
  SetLength(detections, 0);

  // 1. Map defend knight tile → mainscreen
  tileMS := Self.RSW.GetTileMS(DEFEND_KNIGHT_BOX.Middle());
  if (tileMS.Top.X = 0) and (tileMS.Top.Y = 0) then
    Exit;

  centerMS := tileMS.Mean;

  // 2. Dynamic search radius from zoom
  zoom := MM2MS.ZoomLevel / 100.0;
  zoom := EnsureRange(zoom, 0.0, 1.0);
  radius := Round(100 + (zoom * 70));

  // 3. Build color profiles based on combat level
  if Self.OurCmbtLvl >= 100 then
  begin
    // VETERAN
    SetLength(Profiles, 2);

    // Yellow
    Profiles[0].C1 := CTS2(2347498, 12, 0.02, 1.15);
    Profiles[0].C2 := CTS2(2177617, 4,  0.20, 0.90);

    // White
    Profiles[1].C1 := CTS2(12963018, 3, 0.97, 0.47);
    Profiles[1].C2 := CTS2(9406348,  3, 0.46, 0.26);
  end
  else if Self.OurCmbtLvl >= 70 then
  begin
    // INTERMEDIATE
    SetLength(Profiles, 3);

    // Green
    Profiles[0].C1 := CTS2(2177290, 5, 0.12, 6.09);
    Profiles[0].C2 := CTS2(3822370, 4, 0.15, 0.81);

    // Black
    Profiles[1].C1 := CTS2(5066333, 5, 0.23, 0.33);
    Profiles[1].C2 := CTS2(5263713, 4, 0.30, 0.26);

    // Yellow
    Profiles[2].C1 := CTS2(2347498, 12, 0.02, 1.15);
    Profiles[2].C2 := CTS2(2177617, 4,  0.20, 0.90);
  end
  else if Self.OurCmbtLvl >= 40 then
  begin
    // NOVICE
    SetLength(Profiles, 2);

    // Red
    Profiles[0].C1 := CTS2(2107241, 5, 0.09, 0.60);
    Profiles[0].C2 := CTS2(2173038, 4, 0.11, 0.67);

    // Green
    Profiles[1].C1 := CTS2(2177290, 5, 0.12, 6.09);
    Profiles[1].C2 := CTS2(3822370, 4, 0.15, 0.81);
  end
  else
  begin
    Self.Msg('[x0!2x] CmbtLVL: ' + ToStr(Self.OurCmbtLvl));
    TerminateScript('Combat level matters');
  end;

  // 4. Run detection for all profiles tied to this boat
  for P in Profiles do
    ProcessProfile(P);

  if DEBUG_SHIFTERS then
    DebugFoundShifters(detections, centerMS, radius);

  Result := detections;
end;


{********************************************}
{                 DEFEND LOOP                }
{********************************************}
procedure TJdsPestControl.DoDefend();
var
  shifters: TShifterDetectionArray;
  sh: TShifterDetection;
  idx: UInt32;
  theRadius, dx, dy: Int32;
  clickPoint: TPoint;
  radius, theta: Double;

  logHeader, logLine: String;
  f: Int32;

const
  EXPAND_PX = 7;

begin
  Self.UpdateXPState();
  if not Self.XPStale() then
    Exit;

  shifters := Self.GetShifters();
  if Length(shifters) < 1 then
  begin
    Wait(50, 100);
    Exit;
  end;

  { === PICK SHIFTER === }
  if Length(shifters) = 1 then
    idx := 0
  else if Length(shifters) = 2 then
  begin
    if Antiban.BioDice(70) then idx := 0
    else if Antiban.BioDice(90) then idx := 1
    else idx := Random(0,1);
  end
  else
  begin
    if Antiban.BioDice(70) then idx := 0
    else if Antiban.BioDice(90) then idx := 1
    else idx := Random(0, High(shifters));
  end;

  sh := shifters[idx];

  { === HUMAN CLICK OFFSET === }
  if MM2MS.GetZoomLevel() < 37 then
    theRadius := 6
  else
    theRadius := 10;

  repeat
    radius := Abs(SRL.GaussRand(0, theRadius / 2));
  until radius <= theRadius;

  theta := Random() * 2 * PI;

  dx := Round(radius * Cos(theta));
  dy := Round(radius * Sin(theta));

  clickPoint := Point(sh.Mean.X + dx, sh.Mean.Y + dy);

  Mouse.Move(clickPoint);
  Wait(60, 120, wdLeft);

  if MainScreen.GetUpText().Contains('ttack') then
  begin
    Antiban.BioClick(MOUSE_LEFT, 2);

    if MainScreen.DidRedClick() then
    begin
      { ------------------------------------------------------- }
      {                LOG SUCCESSFUL ATTACK                    }
      { ------------------------------------------------------- }

      { If file does not exist, create it and write header }
      if not FileExists(SHIFTER_LOG_FILE) then
      begin
        f := CreateFile(SHIFTER_LOG_FILE);
        if f <> -1 then
          CloseFile(f);

        logHeader :=
          'timestamp,zoom,primary_count,secondary_count,' +
          'expand_px,abox_width,abox_height,dist_to_center,mean_x,mean_y';

        WriteFileContents(SHIFTER_LOG_FILE, logHeader + STR_NEW_LINE, true);
      end;

      { Build CSV line }
      logLine := Format(
        '%d,%.2f,%d,%d,%d,%d,%d,%d,%d,%d',
        [
          GetSystemTime(),
          Double(MM2MS.GetZoomLevel()),
          sh.Cluster.Len,
          sh.Secondary.Len,
          EXPAND_PX,
          sh.ABox.X2 - sh.ABox.X1,
          sh.ABox.Y2 - sh.ABox.Y1,
          Distance(Self.CenterTile, sh.Mean),
          sh.Mean.X,
          sh.Mean.Y
        ]
      );

      { Append row }
      WriteFileContents(SHIFTER_LOG_FILE, logLine + STR_NEW_LINE, true);

      Wait(500, 1400);
    end;
  end;
end;



{********************************************}
{           NPC WIN DETECTION                }
{********************************************}

procedure TJdsPestControl.CheckNPCWin();
var
  txt: String;
begin
  // Avoid double counting if chat sits there
  if (GetSystemTime - Self.LastWinTime) < Self.WinCooldown then
    Exit;

  txt := LowerCase(Chat.GetChat());

  if txt.Contains('managed to destroy') then
  begin
    Self.GamesWon += 1;
    Self.LastWinTime := GetSystemTime();
    Self.Msg('Win detected. Total wins: ' + IntToStr(Self.GamesWon));
  end;
end;


{********************************************}
{                PLANK LOGIC                 }
{********************************************}

function TJdsPestControl.CrossPlank(): Boolean;
begin
  Result := False;

  if self.TheGangPlank.Click(Antiban.BioDice(83.7), random(3,7)) then
    if WaitUntil(self.InBoat(), 600, 4000) then
      EXIT(true);
end;


procedure TJdsPestControl.DoAtPlank();
begin
  // Chat only visible here before we leave the island
  Self.CheckNPCWin();

  if antiban.BioDice(25) then
  begin
    chat.ClickContinue(antiban.BioDice(98));
    wait(600, 1250, wdleft);
  end;

  if not Self.CrossPlank() then
  begin
    Self.RSW.WalkBlind(Self.PlankTile.Random(-7,7,True), 4);
    Wait(600, 1250);

    // One more chance to catch the win message
    Self.CheckNPCWin();

    Self.CrossPlank();
  end;
end;


{********************************************}
{               INIT / RUN                   }
{********************************************}

procedure TJdsPestControl.Init(maxActions: UInt32; maxTime: UInt64); override;
begin
  Self.StartTime := GetSystemTime();
  Self.Msg('Initializing JDS Pest Control');

  inherited;

  Self.RSW.SetupRegions([RSRegions.PEST_CONTROL_ISLAND, RSRegions.PEST_CONTROL_OUTPOST], 8);
  Self.RSW.ScreenWalk := False;

  Self.CenterTile     := DEFEND_KNIGHT_BOX.Middle;

  Self.GamesWon    := 0;
  Self.LastWinTime := 0;
  Self.WinCooldown := 60 * 1000; // 1 min

  Self.PointsTotal := 0;
  Self.PointsTime  := 0;

  // Reset HUD timer
  PestHUD.LastUpdate := 0;
end;


procedure TJdsPestControl.Run(maxActions: UInt32; maxTime: UInt64);
begin
  Self.Init(maxActions, maxTime);

  //Use biohash to affect mouse
  ApplyBioMouse();

  // ===== DO LOGIN IF NEEDED =====
  if not rsclient.IsLoggedIn() then
  begin
    PestControl.Msg('Logging in Forced World ' + ToStr(344));
    Login.LoginPlayer(); // no index
    WaitUntil(rsclient.IsLoggedIn(), 600, 15000);
    Wait(650, 1250);
    MainScreen.SetHighestPitch();
    wait(0, 600);
    Antiban.SmallCameraRotation();
  end else if Login.GetCurrentWorld <> 344 then
  begin
    WorldHopper.Hop([344], False);
    WaitUntil(rsclient.IsLoggedIn(), 600, 15000);
    Wait(650, 1250);
    MainScreen.SetHighestPitch();
    wait(0, 600);
    Antiban.SmallCameraRotation();
  end;

  Self.LastXP      := XPBar.Read();
  Self.LastXPTime  := GetSystemTime();
  Self.NextAttackDelay := HumanGauss(2.3, 3.6, 2.9, 0.25, 0.10);
  Self.Msg('AttackDelay: ' + ToStr(Self.NextAttackDelay));

  Self.OurCmbtStyle := Combat.GetCombatStyle;
  Self.OurCmbtLvl := combat.GetCombatLevel;
  Self.Msg('Combat Level: '+ToStr(Self.OurCmbtLvl));
  Self.Msg('Combat Style: '+Combat.GetCombatStyle);

  // Decide plank + cross-plank tile based on combat level.
  if Self.OurCmbtLvl >= 100 then
  begin
    Self.PlankTile      := VETERAN_PLANK_TILE;
    Self.CrossPlankTile := Point(6125, 4699);
  end
  else if Self.OurCmbtLvl >= 70 then
  begin
    Self.PlankTile      := INTER_PLANK_TILE;
    Self.CrossPlankTile := Point(6149, 4735);
  end
  else if Self.OurCmbtLvl >= 40 then
  begin
    Self.PlankTile      := NOVICE_PLANK_TILE;
    Self.CrossPlankTile := Point(6210, 4755);
  end
  else
    TerminateScript('Combat Level: ' + ToStr(self.OurCmbtLvl));

  with Self.TheGangPlank do
  begin
    SetupCoordinates([Self.CrossPlankTile]);
    SetupUpText(['Cross', 'oss G', 'Gang']);
    ShapeArray.SetShape([1,1,1]);
    Finder.Colors := [CTS2(5402243, 8, 0.05, 0.06)];
    ActionUpText := ['Cross'];
  end;

  repeat
    Self.State := Self.GetState();
    Self.SetAction(ToStr(Self.State));

    case Self.State of
      PCS_IN_BOAT:      Self.DoInBoat();
      PCS_WALK_CENTER:  Self.DoWalkCenter();
      PCS_DEFEND:       Self.DoDefend();
      PCS_AT_PLANK:     Self.DoAtPlank();
    else
      Wait(200, 1200, wdLeft);
    end;

    case Self.State of
      PCS_DEFEND,
      PCS_WALK_CENTER:
        Self.DoAntiban(False, False);
    else
        Self.DoAntiban(True, True);
    end;

    PestHUD.UpdateAndDraw();

    //save CPU
    wait(200, 600, wdleft);

  until Self.ShouldStop();
end;


{********************************************}
{           TEXT REPORT + HUD REPORT         }
{********************************************}

function TBaseScript.BuildTextReport(): TStringArray; override;
var
  elapsedTime   : UInt64;
  winsPerHour   : Double;
begin
  Result := [];

  elapsedTime := Self.TimeRunning.ElapsedTime();

  Result += ' Action        : ' + Self.Action;
  Result += ' Runtime       : ' + SRL.MsToTime(elapsedTime, Time_Short).Trim();
  Result += ' Total Exp     : ' + SRL.FormatNumber(XPBar.TotalEarnedXP(), 2);
  Result += ' Exp/Hour      : ' + SRL.FormatNumber(
    NumberPerHour(XPBar.TotalEarnedXP(), elapsedTime), 2
  );

  winsPerHour := NumberPerHour(PestControl.GamesWon, elapsedTime);

  Result += ' Games Won     : ' + IntToStr(PestControl.GamesWon);
  Result += ' Wins / Hour   : ' + SRL.FormatNumber(winsPerHour, 2);
  Result += ' Points / Hour : ' + SRL.FormatNumber(winsPerHour * PointsPerGame, 2);
  Result += ' Next Break    : ' + TimeUntilNextBreak();

  Result += ' PestControl   : Footballjds <3''s waspscripts.com';
end;



{********************************************}
{                 SCRIPT GUI                 }
{********************************************}

{$IFDEF SCRIPT_GUI}
type
  TPestConfig = record(TScriptForm)
    PCInfo: TLabel;
  end;

var
  PestConfig: TPestConfig;

procedure TPestConfig.StartScript(sender: TObject); override;
var
  obj: TJSONObject;
begin
  obj := WLSettings.GetObject('remote_input');
  obj.Put('enabled', True);

  Login.Players[Login.PlayerIndex].Worlds := [344];

  inherited;
end;



procedure TPestConfig.Run(); override;
var
  tab: TTabSheet;
begin
  Self.Setup('Pest Control by Footballjds');
  Self.Start.SetOnClick(@Self.StartScript);

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  with Self.PCInfo do
  begin
    Create(tab);
    SetCaption('Stands at center and defends the knight.' + LINEENDING +
               'Make sure you start next to veteran plank.');
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(TControl.AdjustToDPI(200));
  end;

  Self.CreateAntibanManager();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();

  inherited;
end;
{$ENDIF}


{********************************************}
{                    ENTRY                   }
{********************************************}

begin
  {$IFDEF SCRIPT_GUI}
  PestConfig.Run();
  {$ENDIF}
  PestControl.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.

