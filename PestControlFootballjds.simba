{$UNDEF SCRIPT_ID}
{$DEFINE SCRIPT_ID := 'pest_control_jds'}
{$UNDEF SCRIPT_REVISION}
{$DEFINE SCRIPT_REVISION := '1'}
{$DEFINE SCRIPT_GUI}
program WaspPestControl;
{$I SRL-T/osr.simba}
{$I WaspLib/osr.simba}

var
  GENERATE_DATA_SETS: Boolean = False;
  DEBUG_SHIFTERS: Boolean = False;
  POINTS_GAME: Int32 = 4;

{             END * USER * SETUP
           )  (       (                       (
        ( /(  )\ )    )\ )       *   )        )\ )
   (    )\())(()/(   (()/( (   ` )  /(    (  (()/(
   )\  ((_)\  /(_))   /(_)))\   ( )(_))   )\  /(_))
  ((_)  _((_)(_))_   (_)) ((_) (_(_()) _ ((_)(_))
  | __|| \| | |   \  / __|| __||_   _|| | | || _ \
  | _| | .` | | |) | \__ \| _|   | |  | |_| ||  _/
  |___||_|\_| |___/  |___/|___|  |_|   \___/ |_|
}
const
  SHIFTER_LOG_FILE = 'pest_shifters_log.csv';
  SHIFTER_FAIL_LOG_FILE = 'pest_shifters_fail_log.csv';

  // Boat interiors
  VETERAN_BOAT_BOX: TBox = [6105, 4695, 6120, 4718];
  INTER_BOAT_BOX: TBox = [6129, 4723, 6144, 4746];
  NOVICE_BOAT_BOX: TBox = [6218, 4740, 6233, 4763];
  // Plank tiles
  VETERAN_PLANK_TILE: TPoint = [6130, 4700];
  INTER_PLANK_TILE: TPoint = [6154, 4736];
  NOVICE_PLANK_TILE: TPoint = [6206, 4756];
  // Island areas
  DEFEND_KNIGHT_BOX: TBox = [5741, 5605, 5775, 5639];
  GAME_START_LANDING_BOX: TBox = [5752, 5532, 5773, 5562];

{********************************************}
{                 ENUM STATES                }
{********************************************}
type
  EPestState = (PCS_IN_BOAT, PCS_WALK_CENTER, PCS_DEFEND, PCS_AT_PLANK, PCS_UNKNOWN, PCS_END_SCRIPT);

{********************************************}
{                 SCRIPT RECORD              }
{********************************************}
type
  TJdsPestControl = record(TBaseWalkerScript)
    State: EPestState;
    CenterTile: TPoint;
    PlankTile: TPoint;
    CrossPlankTile: TPoint;
    TheGangPlank: TRSObject;
    LastXP: Int64;
    LastXPTime: UInt64;
    NextAttackDelay: Int32;
    StartTime: UInt64;
    GamesWon: UInt32;
    LastWinTime: UInt64;
    WinCooldown: UInt64;
    PointsTotal: UInt32;
    PointsTime: UInt64;
    OurCmbtLvl: UInt32;
    OurCmbtStyle: String;

    // Config values (from GUI)
    GenerateDataSets: Boolean;
    DebugShifters: Boolean;
    PointsGame: Int32;
  end;

  TPestHUD = record
    LastUpdate: UInt64;
    Text: TStringArray;
    SpiderBmp: Int32;  // Add this - stores the bitmap handle
    SpiderLoaded: Boolean; // Track if loaded
  end;

  TShifterDetection = record
    Mean: TPoint;
    Cluster: TPointArray;
    Secondary: TPointArray;
    ABox: TBox;
  end;
  TShifterDetectionArray = array of TShifterDetection;

var
  PestControl: TJdsPestControl;
  PestHUD: TPestHUD;

procedure ApplyBioMouse();
var
  bh: Double;
begin
  bh := Login.GetPlayerBioHash();
  Mouse.Speed := Round(10 + ((bh - 0.5) * 4));
  Mouse.Speed := EnsureRange(Mouse.Speed, 9, 15);
  Mouse.Gravity := 8 + (bh * 3);
  Mouse.Wind := 4 + (bh * 2);
  Mouse.MissChance := 10 + (bh * 8);
  if bh < 0.33 then
    Mouse.Distribution := MOUSE_DISTRIBUTION_GAUSS
  else if bh < 0.66 then
    Mouse.Distribution := MOUSE_DISTRIBUTION_ROWP
  else
    Mouse.Distribution := MOUSE_DISTRIBUTION_SKEWED;
  Mouse.IdleInterval := 3.9 + (bh * 1.3);
end;

procedure Wait(Min, Max: Double; Weight: EWaitDir = wdMean); override;
var
  adjMin, adjMax, bias: Double;
begin
  bias := (Login.GetPlayerBioHash() - 0.5) * 0.10;
  adjMin := Min + (Min * bias);
  adjMax := Max + (Max * bias);
  case Weight of
    wdLeft: System.Wait(Round(SRL.TruncatedGauss(adjMin, adjMax)));
    wdMean: System.Wait(Round(SRL.NormalRange(adjMin, adjMax)));
    wdRight: System.Wait(Round(SRL.TruncatedGauss(adjMax, adjMin)));
  end;
end;

function HumanGauss(minVal, maxVal, mean, stdev: Double; bioWeight: Double = 0): Int32;
var
  r: Double;
begin
  r := SRL.GaussRand(mean, stdev);
  if bioWeight <> 0 then
    r += (BioHash - 0.5) * bioWeight;
  if r < minVal then r := minVal;
  if r > maxVal then r := maxVal;
  Result := Round(r * 1000);
end;

function TimeUntilNextBreak(): String;
var
  i: Int32;
  soonest: PBreakTask;
  remaining: Int64;
begin
  if Length(Antiban.Breaks) = 0 then
    Exit('N/A');
  soonest := @Antiban.Breaks[0];
  for i := 1 to High(Antiban.Breaks) do
    if Antiban.Breaks[i].NextAtTime < soonest^.NextAtTime then
      soonest := @Antiban.Breaks[i];
  remaining := Round(soonest^.NextAtTime - GetTimeRunning());
  if remaining < 0 then
    remaining := 0;
  Result := SRL.MsToTime(remaining, Time_Short).Trim();
end;

procedure TAntiban.Setup(); override;
begin
  Self.Skills := [ERSSkill.ATTACK, ERSSkill.STRENGTH, ERSSkill.DEFENCE, ERSSkill.HITPOINTS, ERSSkill.TOTAL];
  Self.MinZoom := 25;
  Self.MaxZoom := 50;
  inherited;
end;

procedure TJdsPestControl.Msg(const s: String);
var
  t: UInt64;
  h, m, sec, ms: UInt64;
begin
  t := GetSystemTime() - Self.StartTime;
  h := t div 3600000;
  m := (t div 60000) mod 60;
  sec := (t div 1000) mod 60;
  ms := t mod 1000;
  WriteLn(Format('[%.2d:%.2d:%.2d:%.3d]:[PCjds]: %s', [h, m, sec, ms, s]));
end;

procedure TPestHUD.Free();
begin
  if Self.SpiderLoaded then
  begin
    FreeBitmap(Self.SpiderBmp);
    Self.SpiderLoaded := False;
    WriteLn('[HUD] Spider icon freed');
  end;
end;

// Add this new initialization procedure
procedure TPestHUD.Init();
begin
  Self.LastUpdate := 0;
  Self.SpiderLoaded := False;

  // Load spider icon once
  try
    Self.SpiderBmp := BitmapFromString(68, 75, 'meJztmN1LU2Ecx/+ChQccTjd39O' +
        'R7S+c2kYlu5pxJTLRQKUVDsCSw0JQiyV4kSLKbiFSCVQZCVhgFWiF' +
        'kdmO0GyOILktDDC8Cu+qu7/zFOG5ze5zDczyeL5+rZ+fl+zk855xn' +
        'x+mUIHZ7FbDZKsDy8ooUFeKWXeriPnwClJcfJQqsbYATHvrh74PFp' +
        'T9Sd2SNMlxWfq0Cd9VxEHA5YD4DuHSvH+MomP/088fSbyB130ipqm' +
        '4CbncjqHDVE1rhCeBS76wzBDQp/ZqUy0DqvpGiAJdjJ7/p8nyAytP' +
        'UcjhqCU54BDQpN/wk9/nRntNoz4K03C4gdf0NUYYLx4+CRGFCm/EC' +
        'lJQeAfQq0QrjBMffAxpdj591hQQujeO0QOr6G6IkF4qn4Tnd2hZbO' +
        'fhfniYV0HWBvDwTIIUAUhcPE9VFni4jI97e3n6QYLgKGhqaQU1NPe' +
        'FyVQO73QFUl53M2NjEwMBt0N3dB9rbO0FTUxtRV9cIxAqLi4tSV94' +
        '0SnJZWPg6N/cR9F+5Cbq6LwEyAi2tp4DqspNZW/sLfL4vs7PzwOf7' +
        'DN68/QCePpsmUvhOoEttB+TifTz/8vV3IHX9DVFd5OlC8Xhqg14cQ' +
        'KsrF0ODupx3YJ9+iJYEUhcPEyW5hE2oXYBE/i79HZC6I2uU4RLBQn' +
        'WRMLvapafnGn0Toy8wUV3kvOZXkktHx/miIheg7zBRFUymYrO5FNB' +
        '/aqK5uZVQXbYf9ukUwUUmE2+XurgqGwiaTrFZBEFfOFlcDIY0kJSU' +
        'Cug6qC7Kc6FnFBAEExCfXa9P5/ksIN6SeoZWBQcLSuhOyc21gQguY' +
        'a9AUpIRqC4Wi5+sLDPB8/tB2LOICxQXVxJUmyYGLQxsRS6r7RCIep' +
        'CoP+1lF5Z7M3R7ekQAcjGZ7KCsrAbk5FgzM82A/SmxWZ+96TI4eAs' +
        'EHZYGxUxOvpqZeQ+8D8YBbZadXUgUWhzAaMwGNGIwZOj1AqAt04U8' +
        'EJjDYc+1WSt2F8aL1tJ6+sLF60C8tkxO5gm6UyLsXmh1gtJSD8F42' +
        'fegC/s0DkL8ZmHfS7zyDyw12S/4XnAJOot4kN037EqendX1qC6hDV' +
        'kGIxjR2oy9fNChtlNVdZGzS2w/xZb8fIvqwtgq7oW32kd1YRncyVC' +
        'BqalpYkt7sQzuZJTkQhkeHiW2cxDVJe6Ji4tMstXnmJyzHRe5XQfV' +
        'RZ5RXeQZ1UWeic1FMfpOZbn8A2OplHA=');
    Self.SpiderLoaded := (Self.SpiderBmp > 0);
    if Self.SpiderLoaded then
      WriteLn('[HUD] Spider icon loaded successfully')
    else
      WriteLn('[HUD] Spider icon failed to load');
  except
    WriteLn('[HUD] Exception loading spider icon');
    Self.SpiderLoaded := False;
  end;
  AddOnTerminate(@Self.Free);
end;

procedure TPestHUD.UpdateAndDraw();
const
  BG_COLOR = $151515;
  BORDER_COLOR = $CC2A2A;
  HEADER_COLOR = $D6B43A;
  TEXT_COLOR = $E0E0E0;
var
  elapsed: UInt64;
  winsPerHour: Double;
  i, lineHeight: Int32;
  abox: TBox;
  x, y: Int32;
begin
  if (GetSystemTime - LastUpdate) < 1000 then
    Exit;
  LastUpdate := GetSystemTime;

  Text := [];
  elapsed := PestControl.TimeRunning.ElapsedTime();
  winsPerHour := NumberPerHour(PestControl.GamesWon, elapsed);
  Text += 'Pest Control - by Footballjds | Total Points: ' + ToStr(PestControl.PointsTotal);
  Text += 'Runtime: ' + SRL.MsToTime(elapsed, Time_Short).Trim();
  Text += 'State: ' + ToStr(PestControl.State);
  Text += 'Wins: ' + IntToStr(PestControl.GamesWon);
  Text += 'Wins/hr: ' + SRL.FormatNumber(winsPerHour, 2);
  Text += 'Points/hr: ' + SRL.FormatNumber(winsPerHour * PestControl.PointsGame, 2);
  Text += 'Total EXP: ' + SRL.FormatNumber(XPBar.TotalEarnedXP(), 2);
  Text += 'XP/hr: ' + SRL.FormatNumber(NumberPerHour(XPBar.TotalEarnedXP(), elapsed), 2);
  Text += 'Next Break: ' + TimeUntilNextBreak();

  lineHeight := 14;
  abox := Box(7, 345, 255, 473);
  RSClient.Image().DrawBoxFilled(abox.Expand(2), False, BORDER_COLOR);
  RSClient.Image().DrawBoxFilled(abox, False, BG_COLOR);

  if Self.SpiderLoaded then
  begin
    try
      RSClient.Image().DrawBitmap(GetMufasaBitmap(Self.SpiderBmp), Point(abox.X1 + 180, abox.Y1 + 20));
    except
      WriteLn('[HUD] Error drawing spider icon');
    end;
  end;

  RSClient.Image().SetFontName('Arial');
  RSClient.Image().SetFontSize(11);
  x := abox.X1 + 3;
  y := abox.Y1 + 3;
  for i := 0 to High(Text) do
  begin
    if i = 0 then
      RSClient.Image().DrawText(Text[i], Point(x, y), HEADER_COLOR)
    else
      RSClient.Image().DrawText(Text[i], Point(x, y), TEXT_COLOR);
    y += lineHeight;
  end;
end;

procedure TJdsPestControl.UpdateXPState();
var
  xp: UInt64;
begin
  xp := XPBar.Read();

  if xp <> Self.LastXP then
  begin
    Self.LastXP := xp;
    Self.LastXPTime := GetSystemTime();
    Self.NextAttackDelay := HumanGauss(2.3, 3.6, 2.9, 0.25, 0.10);
  end;
end;

function TJdsPestControl.XPStale(): Boolean;
begin
  Result := (GetSystemTime - Self.LastXPTime) > Self.NextAttackDelay;
end;

function TJdsPestControl.InBoat(): Boolean;
var
  _ourPos: TPoint;
begin
  _ourPos := self.rsw.GetMyPos();
  result := _ourPos.InBox(VETERAN_BOAT_BOX) or _ourPos.InBox(INTER_BOAT_BOX) or _ourPos.InBox(NOVICE_BOAT_BOX);
end;

function TJdsPestControl.GetState(): EPestState;
var
  pos: TPoint;
begin
  pos := Self.RSW.GetMyPos();

  if pos.InBox(RSRegions.PEST_CONTROL_OUTPOST) then
  begin
    if pos.InBox(VETERAN_BOAT_BOX) or pos.InBox(INTER_BOAT_BOX) or pos.InBox(NOVICE_BOAT_BOX) then
      Exit(PCS_IN_BOAT);
    if (Distance(pos, VETERAN_PLANK_TILE) <= 5) or (Distance(pos, INTER_PLANK_TILE) <= 5) or (Distance(pos, NOVICE_PLANK_TILE) <= 5) then
      Exit(PCS_AT_PLANK);
    Exit(PCS_AT_PLANK);
  end;

  if pos.InBox(RSRegions.PEST_CONTROL_ISLAND) then
  begin
    if pos.InBox(GAME_START_LANDING_BOX) then
      Exit(PCS_WALK_CENTER);
    if pos.InBox(DEFEND_KNIGHT_BOX) then
      Exit(PCS_DEFEND);
    Exit(PCS_WALK_CENTER);
  end;

  Result := PCS_UNKNOWN;
end;

{********************************************}
{                  ACTIONS                   }
{********************************************}
procedure TJdsPestControl.UpdatePoints();
var
  BluePoints: Int32;
const
  CHECK_INTERVAL: Int32 = 60000;
  PEST_POINTS_BOX: Tbox = [72, 64, 123, 81];
begin
  if GetSystemTime < (CHECK_INTERVAL + Self.PointsTime) then
    EXIT;
  BluePoints := ocr.RecognizeNumber(PEST_POINTS_BOX, TOCRColorFilter.Create([16777113]), RS_FONT_PLAIN_12);
  if BluePoints <= Self.PointsTotal then
    Exit;
  Self.PointsTotal := BluePoints;
  Self.PointsTime := GetSystemTime;
  Self.Msg('Total Points: ' + toStr(Self.PointsTotal));
end;

procedure TJdsPestControl.DoInBoat();
begin
  self.UpdatePoints();
  Wait(300, 1500, wdleft);
end;

procedure TJdsPestControl.DoWalkCenter();
begin
  if Self.RSW.AtTile(Self.CenterTile, 11) then
  begin
    Minimap.EnablePrayer();
    Exit;
  end;
  if Antiban.BioDice(25) then
  begin
    Minimap.EnablePrayer();
    Wait(10, 230, wdLeft);
  end;
  Self.RSW.WalkBlind(Self.CenterTile.Random(-7, 7, True), 10);
  Minimap.EnablePrayer();
end;

procedure TJdsPestControl.DebugFoundShifters(const detections: TShifterDetectionArray; const centerMS: TPoint; const radius: Int32);
var
  sh: TShifterDetection;
  R: UInt32;
begin
  RSClient.Image.Clear();
  if MM2MS.GetZoomLevel() < 37 then
    R := 7
  else
    R := 11;
  for sh in detections do
  begin
    RSClient.Image.DrawTPA(sh.Cluster, $FFFF00);
    RSClient.Image.DrawTPA(sh.Secondary, $00FF00);
    RSClient.Image.DrawBox(sh.ABox, $00A5FF);
    RSClient.Image.DrawCircle(sh.Mean, R, $00FFFF);
    RSClient.Image.DrawText(Format('P:%d S:%d', [sh.Cluster.Len, sh.Secondary.Len]), Point(sh.Mean.X + 8, sh.Mean.Y - 8), $FFFFFF);
  end;
  RSClient.Image.DrawCircle(centerMS, radius, $FF00FF);
end;

function TJdsPestControl.GetShifters(): TShifterDetectionArray;
type
  TColorPair = record
    C1, C2: TCTS2Color;
  end;
var
  Profiles: array of TColorPair;
  P: TColorPair;
  primaryTPA, secondTPA: TPointArray;
  clusters: T2DPointArray;
  c: TPointArray;
  abox: TBox;
  tileMS: TRectangle;
  centerMS: TPoint;
  zoom: Double;
  radius: Int32;
  detections: TShifterDetectionArray;
  sh: TShifterDetection;
const
  EXPAND_PX = 7;
  MIN_PRIMARY = 7;
  MIN_SECOND = 7;

  procedure ProcessProfile(const CP: TColorPair);
  begin
    primaryTPA := [];
    SRL.FindColors(primaryTPA, CP.C1, MainScreen.Bounds);
    if primaryTPA = [] then
      Exit;
    clusters := primaryTPA.Cluster(6);
    for c in clusters do
    begin
      if c.Len < MIN_PRIMARY then
        Continue;
      abox := c.Bounds().Expand(EXPAND_PX);
      abox := abox.EnsureRange(MainScreen.Bounds);
      secondTPA := [];
      SRL.FindColors(secondTPA, CP.C2, abox);
      if secondTPA.Len >= MIN_SECOND then
      begin
        sh.Mean := c.Mean();
        sh.Cluster := c;
        sh.Secondary := secondTPA;
        sh.ABox := abox;
        if centerMS.DistanceTo(sh.Mean) <= radius then
        begin
          SetLength(detections, Length(detections) + 1);
          detections[High(detections)] := sh;
        end;
      end;
    end;
  end;

begin
  SetLength(detections, 0);
  tileMS := Self.RSW.GetTileMS(DEFEND_KNIGHT_BOX.Middle());
  if (tileMS.Top.X = 0) and (tileMS.Top.Y = 0) then
    Exit;
  centerMS := tileMS.Mean;
  zoom := MM2MS.ZoomLevel / 100.0;
  zoom := EnsureRange(zoom, 0.0, 1.0);
  radius := Round(100 + (zoom * 70));

  if Self.OurCmbtLvl >= 100 then
  begin
    SetLength(Profiles, 2);
    Profiles[0].C1 := CTS2(2347498, 12, 0.02, 1.15);
    Profiles[0].C2 := CTS2(2177617, 4, 0.20, 0.90);
    Profiles[1].C1 := CTS2(12963018, 3, 0.97, 0.47);
    Profiles[1].C2 := CTS2(9406348, 3, 0.46, 0.26);
  end
  else if Self.OurCmbtLvl >= 70 then
  begin
    SetLength(Profiles, 3);
    Profiles[0].C1 := CTS2(2177290, 5, 0.12, 6.09);
    Profiles[0].C2 := CTS2(3822370, 4, 0.15, 0.81);
    Profiles[1].C1 := CTS2(5066333, 5, 0.23, 0.33);
    Profiles[1].C2 := CTS2(5263713, 4, 0.30, 0.26);
    Profiles[2].C1 := CTS2(2347498, 12, 0.02, 1.15);
    Profiles[2].C2 := CTS2(2177617, 4, 0.20, 0.90);
  end
  else if Self.OurCmbtLvl >= 40 then
  begin
    SetLength(Profiles, 2);
    Profiles[0].C1 := CTS2(2107241, 5, 0.09, 0.60);
    Profiles[0].C2 := CTS2(2173038, 4, 0.11, 0.67);
    Profiles[1].C1 := CTS2(2177290, 5, 0.12, 6.09);
    Profiles[1].C2 := CTS2(3822370, 4, 0.15, 0.81);
  end
  else
  begin
    Self.Msg('[x0!2x] CmbtLVL: ' + ToStr(Self.OurCmbtLvl));
    TerminateScript('Combat level matters');
  end;

  for P in Profiles do
    ProcessProfile(P);

  if Self.DebugShifters then
    DebugFoundShifters(detections, centerMS, radius);

  Result := detections;
end;

procedure TJdsPestControl.LogShifterFail(sh: TShifterDetection; const uptext: String);
var
  logHeader, logLine: String;
  f: Int32;
  cleanUptext: String;
const
  EXPAND_PX = 7;
begin
  if not FileExists(SHIFTER_FAIL_LOG_FILE) then
  begin
    f := CreateFile(SHIFTER_FAIL_LOG_FILE);
    if f <> -1 then
      CloseFile(f);
    logHeader := 'timestamp,zoom,primary_count,secondary_count,' + 'expand_px,abox_width,abox_height,dist_to_center,mean_x,mean_y,uptext';
    WriteFileContents(SHIFTER_FAIL_LOG_FILE, logHeader + STR_NEW_LINE, true);
  end;

  cleanUptext := StringReplace(uptext, ',', ';', [rfReplaceAll]);
  cleanUptext := StringReplace(cleanUptext, #13, ' ', [rfReplaceAll]);
  cleanUptext := StringReplace(cleanUptext, #10, ' ', [rfReplaceAll]);

  logLine := Format('%d,%.2f,%d,%d,%d,%d,%d,%d,%d,%d,%s', [GetSystemTime(), Double(MM2MS.GetZoomLevel()), sh.Cluster.Len, sh.Secondary.Len, EXPAND_PX, sh.ABox.X2 - sh.ABox.X1, sh.ABox.Y2 - sh.ABox.Y1, Distance(Self.CenterTile, sh.Mean), sh.Mean.X, sh.Mean.Y, cleanUptext]);

  WriteFileContents(SHIFTER_FAIL_LOG_FILE, logLine + STR_NEW_LINE, true);
end;

{********************************************}
{                 DEFEND LOOP                }
{********************************************}
procedure TJdsPestControl.DoDefend();
var
  shifters: TShifterDetectionArray;
  sh: TShifterDetection;
  idx: UInt32;
  theRadius, dx, dy: Int32;
  clickPoint: TPoint;
  radius, theta: Double;
  logHeader, logLine, theUpText: String;
  f: Int32;
const
  EXPAND_PX = 7;
begin
  Self.UpdateXPState();
  if not Self.XPStale() then
    Exit;

  shifters := Self.GetShifters();
  if Length(shifters) < 1 then
  begin
    Wait(50, 100);
    Exit;
  end;

  if Length(shifters) = 1 then
    idx := 0
  else if Length(shifters) = 2 then
  begin
    // Some players always attack closest, others pick randomly
    if BioHash < 0.6 then  // 60% prefer closest
      idx := 0
    else
      idx := Random(0, 1);
  end
  else if Length(shifters) > 2 then
  begin
    // Gaussian weighted toward front (closest)
    idx := Round(Abs(SRL.GaussRand(0, High(shifters) * (0.3 + BioHash * 0.4))));
    idx := EnsureRange(idx, 0, High(shifters));
  end;

  sh := shifters[idx];

  if MM2MS.GetZoomLevel() < 37 then
    theRadius := 6
  else
    theRadius := 10;

  theRadius := Round(theRadius * (0.8 + BioHash * 0.4));

  repeat
    radius := Abs(SRL.GaussRand(0, theRadius * (0.4 + BioHash * 0.3)));
  until radius <= theRadius;

  theta := Random() * 2 * PI;
  dx := Round(radius * Cos(theta));
  dy := Round(radius * Sin(theta));
  clickPoint := Point(sh.Mean.X + dx, sh.Mean.Y + dy);

  Mouse.Move(clickPoint);
  Wait(80, 150, wdLeft);
  theUpText := MainScreen.GetUpText();

  if theUpText.Contains('ttack') then
  begin
    Antiban.BioClick(MOUSE_LEFT, 2);
    if MainScreen.DidRedClick() then
    begin
      if Self.GenerateDataSets then
      begin
        if not FileExists(SHIFTER_LOG_FILE) then
        begin
          f := CreateFile(SHIFTER_LOG_FILE);
          if f <> -1 then
            CloseFile(f);
          logHeader := 'timestamp,zoom,primary_count,secondary_count,' + 'expand_px,abox_width,abox_height,dist_to_center,mean_x,mean_y';
          WriteFileContents(SHIFTER_LOG_FILE, logHeader + STR_NEW_LINE, true);
        end;

        logLine := Format('%d,%.2f,%d,%d,%d,%d,%d,%d,%d,%d', [GetSystemTime(), Double(MM2MS.GetZoomLevel()), sh.Cluster.Len, sh.Secondary.Len, EXPAND_PX, sh.ABox.X2 - sh.ABox.X1, sh.ABox.Y2 - sh.ABox.Y1, Distance(Self.CenterTile, sh.Mean), sh.Mean.X, sh.Mean.Y]);

        WriteFileContents(SHIFTER_LOG_FILE, logLine + STR_NEW_LINE, true);
      end;
      Wait(500, 1400);
    end;
  end
  else
  begin
    if Self.GenerateDataSets and (theUpText <> 'Walk here') then
      Self.LogShifterFail(sh, theUpText);
  end;
end;

{********************************************}
{           NPC WIN DETECTION                }
{********************************************}
procedure TJdsPestControl.CheckNPCWin();
var
  txt: String;
begin
  if (GetSystemTime - Self.LastWinTime) < Self.WinCooldown then
    Exit;
  txt := LowerCase(Chat.GetChat());
  if txt.Contains('managed to destroy') then
  begin
    Self.GamesWon += 1;
    Self.LastWinTime := GetSystemTime();
    Self.Msg('Win detected. Total wins: ' + IntToStr(Self.GamesWon));
  end;
end;

{********************************************}
{                PLANK LOGIC                 }
{********************************************}
function TJdsPestControl.CrossPlank(): Boolean;
begin
  Result := False;
  if self.TheGangPlank.Click(Antiban.BioDice(83.7), random(3, 7)) then
    if WaitUntil(self.InBoat(), 600, 4000) then
      EXIT(true);
end;

procedure TJdsPestControl.DoAtPlank();
begin
  Self.CheckNPCWin();
  if antiban.BioDice(25) then
  begin
    chat.ClickContinue(antiban.BioDice(98));
    wait(600, 1250, wdleft);
  end;
  if not Self.CrossPlank() then
  begin
    Self.RSW.WalkBlind(Self.PlankTile.Random(-7, 7, True), 4);
    Wait(600, 1250);
    Self.CheckNPCWin();
    Self.CrossPlank();
  end;
end;

{********************************************}
{               INIT / RUN                   }
{********************************************}
procedure TJdsPestControl.Init(maxActions: UInt32; maxTime: UInt64); override;
begin
  Self.StartTime := GetSystemTime();
  Self.Msg('Initializing JDS Pest Control');
  inherited;

  // Transfer config values from globals
  Self.GenerateDataSets := GENERATE_DATA_SETS;
  Self.DebugShifters := DEBUG_SHIFTERS;
  Self.PointsGame := POINTS_GAME;

  Self.Msg('Config: Points=' + ToStr(Self.PointsGame) + ', DataSets=' + ToStr(Self.GenerateDataSets) + ', Debug=' + ToStr(Self.DebugShifters));

  Self.RSW.SetupRegions([RSRegions.PEST_CONTROL_ISLAND, RSRegions.PEST_CONTROL_OUTPOST], 8);
  Self.RSW.ScreenWalk := False;
  Self.CenterTile := DEFEND_KNIGHT_BOX.Middle;
  Self.GamesWon := 0;
  Self.LastWinTime := 0;
  Self.WinCooldown := 60 * 1000;
  Self.PointsTotal := 0;
  Self.PointsTime := 0;
  PestHUD.Init(); // Initialize HUD with spider ico
end;

procedure TJdsPestControl.Run(maxActions: UInt32; maxTime: UInt64);
begin
  Self.Init(maxActions, maxTime);
  ApplyBioMouse();

  if not rsclient.IsLoggedIn() then
  begin
    PestControl.Msg('Logging in Forced World ' + ToStr(344));
    Login.LoginPlayer();
    WaitUntil(rsclient.IsLoggedIn(), 600, 15000);
    Wait(650, 1250);
    MainScreen.SetHighestPitch();
    wait(0, 600);
    Antiban.SmallCameraRotation();
  end
  else if Login.GetCurrentWorld <> 344 then
  begin
    WorldHopper.Hop([344], False);
    WaitUntil(rsclient.IsLoggedIn(), 600, 15000);
    Wait(650, 1250);
    MainScreen.SetHighestPitch();
    wait(0, 600);
    Antiban.SmallCameraRotation();
  end;

  Self.LastXP := XPBar.Read();
  Self.LastXPTime := GetSystemTime();
  Self.NextAttackDelay := HumanGauss(2.3, 3.6, 2.9, 0.25, 0.10);
  Self.Msg('AttackDelay: ' + ToStr(Self.NextAttackDelay));
  Self.OurCmbtStyle := Combat.GetCombatStyle;
  Self.OurCmbtLvl := combat.GetCombatLevel;
  Self.Msg('Combat Level: ' + ToStr(Self.OurCmbtLvl));
  Self.Msg('Combat Style: ' + Combat.GetCombatStyle);
  Self.Msg('Auto retaliate active: ' + ToStr(Combat.SetAutoRetaliate(False)));

  if Self.OurCmbtLvl >= 100 then
  begin
    Self.PlankTile := VETERAN_PLANK_TILE;
    Self.CrossPlankTile := Point(6125, 4699);
  end
  else if Self.OurCmbtLvl >= 70 then
  begin
    Self.PlankTile := INTER_PLANK_TILE;
    Self.CrossPlankTile := Point(6149, 4735);
  end
  else if Self.OurCmbtLvl >= 40 then
  begin
    Self.PlankTile := NOVICE_PLANK_TILE;
    Self.CrossPlankTile := Point(6210, 4755);
  end
  else
    TerminateScript('Combat Level: ' + ToStr(self.OurCmbtLvl));

  with Self.TheGangPlank do
  begin
    SetupCoordinates([Self.CrossPlankTile]);
    SetupUpText(['Cross', 'oss G', 'Gang']);
    ShapeArray.SetShape([1, 1, 1]);
    Finder.Colors := [CTS2(5402243, 8, 0.05, 0.06)];
    ActionUpText := ['Cross'];
  end;

  repeat
    Self.State := Self.GetState();
    Self.SetAction(ToStr(Self.State));
    case Self.State of
      PCS_IN_BOAT: Self.DoInBoat();
      PCS_WALK_CENTER: Self.DoWalkCenter();
      PCS_DEFEND: Self.DoDefend();
      PCS_AT_PLANK: Self.DoAtPlank();
      else
        Wait(200, 1200, wdLeft);
    end;
    case Self.State of
      PCS_DEFEND, PCS_WALK_CENTER: Self.DoAntiban(False, False);
      else
        Self.DoAntiban(True, True);
    end;
    PestHUD.UpdateAndDraw();
    wait(200, 600, wdleft);
  until Self.ShouldStop();
end;

{********************************************}
{           TEXT REPORT + HUD REPORT         }
{********************************************}
function TBaseScript.BuildTextReport(): TStringArray; override;
var
  elapsedTime: UInt64;
  winsPerHour: Double;
begin
  Result := [];
  elapsedTime := Self.TimeRunning.ElapsedTime();
  Result += ' Action        : ' + Self.Action;
  Result += ' Runtime       : ' + SRL.MsToTime(elapsedTime, Time_Short).Trim();
  Result += ' Total Exp     : ' + SRL.FormatNumber(XPBar.TotalEarnedXP(), 2);
  Result += ' Exp/Hour      : ' + SRL.FormatNumber(NumberPerHour(XPBar.TotalEarnedXP(), elapsedTime), 2);
  winsPerHour := NumberPerHour(PestControl.GamesWon, elapsedTime);
  Result += ' Games Won     : ' + IntToStr(PestControl.GamesWon);
  Result += ' Wins / Hour   : ' + SRL.FormatNumber(winsPerHour, 2);
  Result += ' Points / Hour : ' + SRL.FormatNumber(winsPerHour * PestControl.PointsGame, 2);
  Result += ' Next Break    : ' + TimeUntilNextBreak();
  Result += ' PestControl   : Footballjds <3' 's waspscripts.com';
end;

{$IFDEF SCRIPT_GUI}

type
  TPestConfig = record(TScriptForm)
    PCInfo: TLabel;

    GameSettingsPanel: TLabeledPanel;
    DebugSettingsPanel: TLabeledPanel;

    PointsGameCombo: TLabeledComboBox;

    GenerateDataCheckBox: TLabeledCheckBox;
    DebugShiftersCheckBox: TLabeledCheckBox;

    Config: TConfigJSON;
  end;

var
  PestConfig: TPestConfig;

procedure TPestConfig.LoadConfig();
begin
  Self.Config.Setup('pest-control-jds');

  POINTS_GAME := Self.Config.GetInt('points_game');
  if (POINTS_GAME < 3) or (POINTS_GAME > 8) then
    POINTS_GAME := 4;

  GENERATE_DATA_SETS := Self.Config.GetBoolean('generate_data_sets');
  DEBUG_SHIFTERS := Self.Config.GetBoolean('debug_shifters');

  WriteLn('[Config] Loaded: Points=', POINTS_GAME, ', DataSets=', GENERATE_DATA_SETS, ', Debug=', DEBUG_SHIFTERS);
end;

procedure TPestConfig.SaveSettings();
begin
  Self.Config.Put('points_game', POINTS_GAME);
  Self.Config.Put('generate_data_sets', GENERATE_DATA_SETS);
  Self.Config.Put('debug_shifters', DEBUG_SHIFTERS);
  WriteLn('[Config] Saved: Points=', POINTS_GAME, ', DataSets=', GENERATE_DATA_SETS, ', Debug=', DEBUG_SHIFTERS);
end;

procedure TPestConfig.PointsGameChanged(sender: TObject);
begin
  POINTS_GAME := Self.PointsGameCombo.GetItemIndex() + 3;
  WriteLn('[GUI] Points per game changed to: ', POINTS_GAME);
  Self.SaveSettings();
end;

procedure TPestConfig.GenerateDataChanged(sender: TObject);
begin
  GENERATE_DATA_SETS := Self.GenerateDataCheckBox.IsChecked();
  WriteLn('[GUI] Generate data sets: ', GENERATE_DATA_SETS);
  Self.SaveSettings();
end;

procedure TPestConfig.DebugShiftersChanged(sender: TObject);
begin
  DEBUG_SHIFTERS := Self.DebugShiftersCheckBox.IsChecked();
  WriteLn('[GUI] Debug shifters: ', DEBUG_SHIFTERS);
  Self.SaveSettings();
end;

procedure TPestConfig.StartScript(sender: TObject); override;
var
  obj: TJSONObject;
begin
  Self.SaveSettings();

  obj := WLSettings.GetObject('remote_input');
  obj.Put('enabled', True);
  Login.Players[Login.PlayerIndex].Worlds := [344];

  WriteLn('[Script] Starting with Points=', POINTS_GAME, ', DataSets=', GENERATE_DATA_SETS, ', Debug=', DEBUG_SHIFTERS);

  inherited;
end;

procedure TPestConfig.Run(); override;
var
  tab: TTabSheet;
  nextTop: Int32;
begin
  Self.Setup('Pest Control by Footballjds');
  Self.Start.SetOnClick(@Self.StartScript);

  Self.LoadConfig();

  Self.AddTab('Script Settings');
  tab := Self.Tabs[High(Self.Tabs)];

  Self.CreateAccountManager(tab);

  nextTop := TControl.AdjustToDPI(170);

  // Info text
  with Self.PCInfo do
  begin
    Create(tab);
    SetCaption('Stands at center and defends the knight.' + LINEENDING +
                'Script detects combat level and uses appropriate boat.');
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(nextTop);
    SetAutoSize(True);
    GetFont().setName('Comic Sans MS');
    GetFont().SetSize(14);
    GetFont().SetStyle([fsBold]);
  end;

  nextTop += TControl.AdjustToDPI(55);

  // Game Settings Panel
  Self.GameSettingsPanel.Create(tab);
  Self.GameSettingsPanel.SetCaption('Game Settings');
  Self.GameSettingsPanel.SetLeft(TControl.AdjustToDPI(30));
  Self.GameSettingsPanel.SetTop(nextTop);
  Self.GameSettingsPanel.SetWidth(TControl.AdjustToDPI(350));
  Self.GameSettingsPanel.SetHeight(TControl.AdjustToDPI(75));
  Self.GameSettingsPanel.Caption.SetFontSize(12);


  // Remove all borders and bevels
  Self.GameSettingsPanel.Panel.SetBorderStyle(bsNone);
  Self.GameSettingsPanel.Panel.SetBevelInner(bvNone);
  Self.GameSettingsPanel.Panel.SetBevelOuter(bvNone);

  with Self.PointsGameCombo do
  begin
    Create(Self.GameSettingsPanel.Panel);
    SetCaption('Points per Game:');
    SetLeft(TControl.AdjustToDPI(15));
    SetTop(TControl.AdjustToDPI(25));
    SetWidth(TControl.AdjustToDPI(150));
    SetStyle(csDropDownList);
    AddItemArray(['3', '4', '5', '6', '7', '8']);
    SetItemIndex(POINTS_GAME - 3);
    SetHint('Select points awarded per game (depends on boat level and diary)');
    ComboBox.SetOnChange(@Self.PointsGameChanged);
    setName('Courier New');
  end;

  nextTop += Self.GameSettingsPanel.GetHeight + TControl.AdjustToDPI(10);

  // Debug Settings Panel
  Self.DebugSettingsPanel.Create(tab);
  Self.DebugSettingsPanel.SetCaption('Debug Settings (Advanced Users)');
  Self.DebugSettingsPanel.SetLeft(TControl.AdjustToDPI(30));
  Self.DebugSettingsPanel.SetTop(nextTop);
  Self.DebugSettingsPanel.SetWidth(TControl.AdjustToDPI(500));
  Self.DebugSettingsPanel.SetHeight(TControl.AdjustToDPI(95));
  Self.DebugSettingsPanel.Caption.SetFontSize(12);

  // Remove all borders and bevels
  Self.DebugSettingsPanel.Panel.SetBorderStyle(bsNone);
  Self.DebugSettingsPanel.Panel.SetBevelInner(bvNone);
  Self.DebugSettingsPanel.Panel.SetBevelOuter(bvNone);

  with Self.GenerateDataCheckBox do
  begin
    Create(Self.DebugSettingsPanel.Panel);
    SetCaption('Generate Data Sets (CSV logs)');
    SetLeft(TControl.AdjustToDPI(15));
    SetTop(TControl.AdjustToDPI(25));
    SetChecked(GENERATE_DATA_SETS);
    SetHint('Save shifter attack detection data to CSV files for analysis');
    CheckBox.SetOnChange(@Self.GenerateDataChanged);
  end;

  with Self.DebugShiftersCheckBox do
  begin
    Create(Self.DebugSettingsPanel.Panel);
    SetCaption('Debug Shifters (Visual overlay)');
    SetLeft(TControl.AdjustToDPI(15));
    SetTop(TControl.AdjustToDPI(55));
    SetChecked(DEBUG_SHIFTERS);
    SetHint('Draw detection boxes, points, and click areas on screen for debugging');
    CheckBox.SetOnChange(@Self.DebugShiftersChanged);
  end;

  Self.CreateAntibanManager();
  Self.CreateWaspLibSettings();
  Self.CreateAPISettings();

  inherited;
end;

{$ENDIF}

{********************************************}
{                    ENTRY                   }
{********************************************}
begin
{$IFDEF SCRIPT_GUI}
  PestConfig.Run();
{$ENDIF}
  PestControl.Run(WLSettings.MaxActions, WLSettings.MaxTime);
end.
